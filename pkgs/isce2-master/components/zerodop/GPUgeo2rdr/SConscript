#!/usr/bin/env python
import os

Import('envzerodop')
envGPUgeo2rdr = envzerodop.Clone()
package = envGPUgeo2rdr['PACKAGE']
project = 'GPUgeo2rdr'
envGPUgeo2rdr['PROJECT'] = project
install = envGPUgeo2rdr['PRJ_SCONS_INSTALL'] + '/' + package + '/' + project

###Fixing current name clash in isceLib and utilLib
util_package_path = package.split('/')
util_package_path = [util_package_path[0],'isceobj','Util','Library','include']
util_package_path = '/'.join(util_package_path)
old_lib_path = os.path.join(envGPUgeo2rdr['PRJ_SCONS_BUILD'],util_package_path)
cpp_path = []
for path in envGPUgeo2rdr['CPPPATH']:
    if path != old_lib_path:
        cpp_path.append(path)
envGPUgeo2rdr['CPPPATH'] = cpp_path

initFile = '__init__.py'
if not os.path.exists(initFile):
    with open(initFile, 'w') as fout:
        fout.write('#!/usr/bin/env python3')

listFiles = [initFile]
envGPUgeo2rdr.Install(install, listFiles)
envGPUgeo2rdr.Alias('install', install)
Export('envGPUgeo2rdr')


build = envGPUgeo2rdr['PRJ_SCONS_BUILD'] + '/' + package + '/' + project + '/src'

envGPUgeo2rdr.Install(build,'GPUgeo2rdr.pyx')
envGPUgeo2rdr.Alias('install', build)

includeScons = os.path.join('include','SConscript')
SConscript(includeScons)

cudaScons = 'cuda/SConscript'
SConscript(cudaScons)

srcScons = os.path.join('src','SConscript')
SConscript(srcScons,variant_dir = build)

