#!/bin/tcsh -f
#
# Can be run in place of make_ifgs when a DEM need not be subtracted
#
# Andy Hooper, Feb 2010
# ===========================================================================
# 06/2011 AH: Create dummy heights file for geocoding
# ===========================================================================

set WORKDIR = ".." 

if ( -e $WORKDIR/looks.txt) then 
    set looks = `cat $WORKDIR/looks.txt` 
else
    set looks = 4
endif

if ( -e $WORKDIR/ar.txt) then 
    set ar = `cat $WORKDIR/ar.txt` 
else
    set ar = 5
endif

@ azlooks = $looks * $ar

#set first = `grep 'First_pixel ' $WORKDIR/master.res | gawk 'END {print $4}'`
#set last = `grep 'Last_pixel '  $WORKDIR/master.res | gawk 'END {print $4}'`
set first = `get_slc_lp $WORKDIR/master.res | awk '{print $3}'`  # Pixel
set last  = `get_slc_lp $WORKDIR/master.res | awk '{print $4}'`

@ width = ($last - $first) + 1

#set first_l = `grep 'First_line ' $WORKDIR/master.res | gawk 'END {print $4}'`
#set last_l = `grep 'Last_line ' $WORKDIR/master.res | gawk 'END {print $4}'`
set first_l = `get_slc_lp $WORKDIR/master.res | awk '{print $1}'` # Line
set last_l  = `get_slc_lp $WORKDIR/master.res | awk '{print $2}'`

@ numlines = ($last_l - $first_l) + 1

if (! -e $WORKDIR/interferogram.dorisin) then
    cp $DORIS_SCR/interferogram.dorisin $WORKDIR
endif

sed -n '1,/SUBTRREFPHA/p' $WORKDIR/interferogram.dorisin > tmp.dorisin
sed -n '/LOGFILE/,$p' $WORKDIR/interferogram.dorisin >> tmp.dorisin
\mv -f tmp.dorisin $WORKDIR/interferogram_nodem.dorisin
                                  
\cp -f coreg.out interferogram.out                                                         
\rm -f cint.minrefdem* >& /dev/null
doris $WORKDIR/interferogram_nodem.dorisin >> step_ifg.log
\mv -f cint.minrefpha.raw cint.minrefdem.raw

if (! -e $WORKDIR/dummy_dem_radar.raw) then
    @ numbytes = $width * 4 
    dd if=/dev/zero of=$WORKDIR/dummy_dem_radar.raw count=$numlines bs=$numbytes
endif
ln -sf $WORKDIR/dummy_dem_radar.raw dem_radar.raw

set endras = l.ras
set endraw = l.raw

cpxfiddle -w$width -fcr4 -qmixed -M$looks/$azlooks -osunraster -e0.2 -s1.8 cint.minrefdem.raw > cint.minrefdem_$looks$endras

cpxfiddle -w$width -fcr4 -qnormal -M$looks/$azlooks -ofloat cint.minrefdem.raw > cint.minrefdem_$looks$endraw
