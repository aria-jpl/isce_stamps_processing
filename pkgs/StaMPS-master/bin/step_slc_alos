#!/bin/tcsh -f
#
# ===========================================================================
# 09/2008 Andy Hooper
# ===========================================================================
#
# ===========================================================================
# 11/2009 AH:  Automatically generate multilooked images for viewing
# ===========================================================================

set WORKDIR = '..'
set dir = `\pwd | gawk 'BEGIN {FS="/"} {print $(NF)}'`

if (! -e $WORKDIR/roi.proc) then
    cp $MY_SCR/roi.proc $WORKDIR
endif

if (! -e $WORKDIR/looks.txt) then
    set looks = 5
    echo $looks > $WORKDIR/looks.txt
else
    set looks = `cat $WORKDIR/looks.txt`
endif
if (! -e $WORKDIR/ar.txt) then
    set ar = 3
    echo $ar > $WORKDIR/ar.txt
else
    set ar = `cat $WORKDIR/ar.txt`
endif
@ azlooks = $looks * $ar

\rm -f $dir.raw >& /dev/null
\rm -f $dir.raw.rsc >& /dev/null
\rm -f $dir.slc >& /dev/null
\rm -f $dir.slc.rsc >& /dev/null

set dir = `\pwd | gawk 'BEGIN {FS="/"} {print $(NF)}'`
set hv = `\ls IMG-HV* | sed 1q` 
if ($hv != "") then
    echo ""
    echo "Looks like dual polarisation"
    echo ""
    make_raw_alos.pl IMG-HH-ALP $dir FBD2FBS > step_slc.log
else
    make_raw_alos.pl IMG-HH-ALP $dir > step_slc.log
endif

echo $MY_SCR
$MY_SCR/roi_prep.pl $dir HDR >> step_slc.log
roi.pl $dir no 1 no >> step_slc.log
roipac2doris $dir s >> step_slc.log

set first = `grep 'First_pixel ' slave.res | gawk 'END {print $4}'`
set last = `grep 'Last_pixel '  slave.res | gawk 'END {print $4}'`
@ width = ($last - $first) + 1

set endras = l.ras
set endraw = l.raw
cpxfiddle -w$width -fcr4 -qmag -M$looks/$azlooks -osunraster -e0.5 -s1.0 $dir.slc > image.slc_$looks$endras

cpxfiddle -w$width -fcr4 -qnormal -M$looks/$azlooks -ofloat $dir.slc > image.slc_$looks$endraw

echo "*******************************************************************" >> slave.res
echo "*_Start_precise_orbits:" >> slave.res
echo "*******************************************************************" >> slave.res
echo "        t(s)    X(m)    Y(m)    Z(m)" >> slave.res
#set numlines = `wc -l hdr_data_points* | gawk '{print $1}'`
#echo "NUMBER_OF_DATAPOINTS:                   " $numlines >> slave.res
echo "NUMBER_OF_DATAPOINTS:                   10" >> slave.res
head -n19 hdr_data_points* | tail -n10 | gawk '{print $1,$2,$3,$4}' >> slave.res
echo "*******************************************************************" >> slave.res
echo "* End_precise_orbits:_NORMAL" >> slave.res
echo "*******************************************************************" >> slave.res

