#!/bin/csh -f
#
# Anneleen Oyen & Andy Hooper, March 2010
# ===========================================================================
# 03/2010 AO&AH:  Create SLC from decoded wideswath data
# ===========================================================================

set WORKDIR = '..'
set dir = `\pwd | gawk 'BEGIN {FS="/"} {print $(NF)}'`

cp $WORKDIR/roi.proc .

if ( -e $WORKDIR/looks.txt) then
    set looks = `cat $WORKDIR/looks.txt`
else
    set looks = 4
endif

if ( -e $WORKDIR/ar.txt) then
    set ar = `cat $WORKDIR/ar.txt`
else
    set ar = 5
endif

@ azlooks = $looks * $ar

set dir = `\pwd | gawk 'BEGIN {FS="/"} {print $(NF)}'`
echo $dir

foreach k(`cat $WORKDIR/make_slcs_ws.list`)

    echo 'Processing subswath '$k

    cd SS$k
    set dir2 = `\pwd | gawk 'BEGIN {FS="/"} {print $(NF-1)}'`
    \rm -f $dir2.slc >& /dev/null
    \rm -f $dir2.slc.rsc >& /dev/null

    $MY_SCR/roi_prep.pl $dir HDR >> step_slc.log
    roi.pl $dir no 1 no >> step_slc.log
    roipac2doris $dir s >> step_slc.log

    set first = `grep 'First_pixel ' slave.res | gawk 'END {print $4}'`
    set last = `grep 'Last_pixel '  slave.res | gawk 'END {print $4}'`
    @ width = ($last - $first) + 1

    set endras = l.ras
    set endraw = l.raw
    cpxfiddle -w$width -fcr4 -qmag -M$looks/$azlooks -osunraster -e0.5 -s1.0 $dir.slc > image.slc_$looks$endras

    cpxfiddle -w$width -fcr4 -qnormal -M$looks/$azlooks -ofloat $dir.slc > image.slc_$looks$endraw

    echo "*******************************************************************" >> slave.res
    echo "*_Start_precise_orbits:" >> slave.res
    echo "*******************************************************************" >> slave.res
    echo "        t(s)    X(m)    Y(m)    Z(m)" >> slave.res
    echo "NUMBER_OF_DATAPOINTS:                   10" >> slave.res
    head -n15 hdr_data_points* | tail -n10 | gawk '{print $1,$2,$3,$4}' >> slave.res
    echo "*******************************************************************" >> slave.res
    echo "* End_precise_orbits:_NORMAL" >> slave.res
    echo "*******************************************************************" >> slave.res

    cd ..
end

