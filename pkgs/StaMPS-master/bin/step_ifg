#!/bin/tcsh -f
#
# Andy Hooper, 2005
# ===========================================================================
# 09/2008 AH: Generalised for variable mutilooking in range and az
# 03/2009 AH: Use refdem direct from Doris
# 08/2009 AH: Include_flatearth card added
# 01/2010 MA: Use get_slc_lp for getting line and pixel coordinates.
# 05/2010 AH: If no DEM subtracted, multilook flat Earth version
# ===========================================================================

set WORKDIR = ".." 

if ( -e $WORKDIR/looks.txt) then 
    set looks = `cat $WORKDIR/looks.txt` 
else
    set looks = 4
endif

if ( -e $WORKDIR/ar.txt) then 
    set ar = `cat $WORKDIR/ar.txt` 
else
    set ar = 5
endif

@ azlooks = $looks * $ar

#set first = `grep 'First_pixel ' $WORKDIR/master.res | gawk 'END {print $4}'`
#set last = `grep 'Last_pixel '  $WORKDIR/master.res | gawk 'END {print $4}'`
set first = `get_slc_lp $WORKDIR/master.res | awk '{print $3}'`  # Pixel
set last  = `get_slc_lp $WORKDIR/master.res | awk '{print $4}'`

@ width = ($last - $first) + 1
                                                                                           
#set first_l = `grep 'First_line ' $WORKDIR/master.res | gawk 'END {print $4}'`
#set last_l = `grep 'Last_line ' $WORKDIR/master.res | gawk 'END {print $4}'`
set first_l = `get_slc_lp $WORKDIR/master.res | awk '{print $1}'` # Line
set last_l  = `get_slc_lp $WORKDIR/master.res | awk '{print $2}'`

@ numlines = ($last_l - $first_l) + 1

if (! -e $WORKDIR/interferogram.dorisin) then
    cp $DORIS_SCR/interferogram.dorisin $WORKDIR
endif
                                                                                           
echo "*****************************************************" > fake_dem.out
echo "*_Start_comp_refdem:" >> fake_dem.out
echo "*****************************************************" >> fake_dem.out
echo "Method:                                 dem gtopo30 (phase w.r.t. ellipsoid.)" >> fake_dem.out
echo "Include_flatearth:                      No" >> fake_dem.out
echo "DEM source file:                        dummy" >> fake_dem.out
echo "Data_output_file:                       refdem_1l.raw" >> fake_dem.out
echo "Data_output_format:                     real4" >> fake_dem.out
echo "First_line (w.r.t. original_master):   " $first_l >> fake_dem.out
echo "Last_line (w.r.t. original_master):    " $last_l >> fake_dem.out
echo "First_pixel (w.r.t. original_master):  " $first >> fake_dem.out
echo "Last_pixel (w.r.t. original_master):   " $last >> fake_dem.out
echo "Multilookfactor_azimuth_direction:      1" >> fake_dem.out
echo "Multilookfactor_range_direction:        1" >> fake_dem.out
echo "Number of lines (multilooked):         " $numlines >> fake_dem.out
echo "Number of pixels (multilooked):        " $width >> fake_dem.out
echo "*****************************************************" >> fake_dem.out
echo "* End_comp_refdem:_NORMAL" >> fake_dem.out
echo "*****************************************************" >> fake_dem.out
cat coreg.out fake_dem.out > interferogram.out
\rm -f fake_dem.out >& /dev/null
\rm -f cint.minrefdem* >& /dev/null
doris $WORKDIR/interferogram.dorisin >> step_ifg.log

set endras = l.ras
set endraw = l.raw
cpxfiddle -w$width -fcr4 -qmixed -M$looks/$azlooks -osunraster -e0.2 -s1.8 cint.minrefpha.raw > cint.minrefpha_$looks$endras

if (-e cint.minrefdem.raw) then
   cpxfiddle -w$width -fcr4 -qmixed -M$looks/$azlooks -osunraster -e0.2 -s1.8 cint.minrefdem.raw > cint.minrefdem_$looks$endras

   cpxfiddle -w$width -fcr4 -qnormal -M$looks/$azlooks -ofloat cint.minrefdem.raw > cint.minrefdem_$looks$endraw
else
   cpxfiddle -w$width -fcr4 -qnormal -M$looks/$azlooks -ofloat cint.minrefpha.raw > cint.minrefpha_$looks$endraw
endif

\rm -f cint.minrefpha.raw
